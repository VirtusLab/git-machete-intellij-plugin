commonsIO()
jcabiAspects()
junit()
lombok()
powerMock()
slf4jLambdaApi()
slf4jTestImpl()
vavr()

dependencies {
  implementation project(':binding')
  implementation project(':gitCore:api')

  api project(':backend:api')
  api project(':branchLayout:api')

  // Note that we can't easily use Gradle's `testFixtures` configuration here
  // as it doesn't seem to expose testFixtures resources in test classpath correctly.
  testImplementation project(':testCommon').sourceSets.test.output
  testRuntimeOnly project(':branchLayout:impl')
  testRuntimeOnly project(':gitCore:jGit')
}

applySubtypingChecker()

task regenerateCliOutputs(type: JavaExec) {
  group = "Execution"
  description = "Regenerate CLI outputs used for comparison in tests"
  classpath = sourceSets.test.runtimeClasspath
  mainClass = "com.virtuslab.gitmachete.backend.integration.RegenerateCliOutputs"
  args = sourceSets.test.resources.srcDirs
}
