buildscript {
  dependencies {
    classpath group: 'org.jsoup', name: 'jsoup', version: '1.15.1'
  }
}

plugins {
  id 'org.gradle.checkstyle'
  id 'org.gradle.java-library'
  id 'org.gradle.scala'

  id 'com.github.ben-manes.versions' version '0.42.0'

  id 'com.diffplug.spotless' version '6.5.0' apply false
  id 'io.freefair.aspectj.post-compile-weaving' version '6.4.3' apply false
  id 'io.freefair.lombok' version '6.4.3' apply false
  id 'org.ajoberstar.grgit' version '5.0.0' apply false
  id 'org.checkerframework' version '0.6.10' apply false
  id 'org.jetbrains.grammarkit' version '2021.2.2' apply false
  id 'org.jetbrains.intellij' version '1.5.3' apply false
  // Minor Kotlin version is effectively enforced by the IntelliJ version we're building against,
  // check https://plugins.jetbrains.com/docs/intellij/kotlin.html#kotlin-standard-library
  // every time `intellijVersions.buildTarget` is bumped.
  id 'org.jetbrains.kotlin.jvm' version '1.6.20' apply false
}

if (JavaVersion.current() != JavaVersion.VERSION_11) {
  throw new GradleException("Project must be built with Java version 11")
}

ext {
  javaMajorVersion = JavaVersion.VERSION_11
  scalaMajorVersion = '2.13'

  archUnitVersion = '0.23.1'
  betterStringsVersion = '0.5'
  checkerFrameworkVersion = '3.21.4'
  checkstyleToolVersion = '8.33'
  commonsIOVersion = '2.11.0'
  ideProbeVersion = '0.26.0'
  jcabiAspectsVersion = '0.23.2'
  jetbrainsAnnotationsVersion = '23.0.0'
  jgitVersion = '6.1.0.202203080745-r'
  junitVersion = '4.13.2'
  lombokVersion = '1.18.24'
  powerMockVersion = '2.0.9'
  reflectionsVersion = '0.10.2'
  slf4jLambdaApiVersion = '0.1'
  slf4jVersion = '1.7.36'
  vavrVersion = '0.10.4'

  intellijSnapshotsUrl = "https://www.jetbrains.com/intellij-repository/snapshots/"
  Properties intellijVersionsProp = intellijVersionsProperties()

  // See https://www.jetbrains.com/intellij-repository/releases/ -> Ctrl+F .idea
  intellijVersions = [
    // When this value is updated, remember to update the minimum required IDEA version in README.md.
    earliestSupportedMajor: intellijVersionsProp.getProperty("earliestSupportedMajor"),
    // Most recent minor versions of all major releases
    // between earliest supported (incl.) and latest stable (excl.), used for binary compatibility checks and UI tests
    latestMinorsOfOldSupportedMajors: Arrays.asList(intellijVersionsProp.getProperty("latestMinorsOfOldSupportedMajors").split(",")),
    latestStable: intellijVersionsProp.getProperty("latestStable"),
    // Note that we have to use a "fixed snapshot" version X.Y.Z-EAP-SNAPSHOT (e.g. 211.4961.33-EAP-SNAPSHOT)
    // rather than a "rolling snapshot" X-EAP-SNAPSHOT (e.g. 211-EAP-SNAPSHOT)
    // to ensure that the builds are reproducible.
    // EAP-CANDIDATE-SNAPSHOTs can be used for binary compatibility checks,
    // but for some reason aren't resolved in UI tests.
    // Generally, see https://www.jetbrains.com/intellij-repository/snapshots/ -> Ctrl+F .idea
    // Use `null` if the latest supported major has a stable release (and not just EAPs).
    eapOfLatestSupportedMajor: intellijVersionsProp.getProperty("eapOfLatestSupportedMajor")
  ]

  intellijVersions.latestSupportedMajor = intellijVersions.eapOfLatestSupportedMajor
    ? buildNumberToIntellijVersion(intellijVersions.eapOfLatestSupportedMajor)
    : majorPartOfIntellijVersion(intellijVersions.latestStable)

  intellijVersions.buildTarget = intellijVersions.eapOfLatestSupportedMajor ?: intellijVersions.latestStable

  ciBranch = System.getenv('CIRCLE_BRANCH')
  isCI = System.getenv('CI') == 'true'
  jetbrainsMarketplaceToken = System.getenv('JETBRAINS_MARKETPLACE_TOKEN')

  // This values can't be named in the same way as their corresponding properties to avoid a name clash.
  jvmArgsForJavaCompilation = project.properties.get('compileJavaJvmArgs')?.tokenize(' ')
  shouldRunAllCheckers = isCI || project.hasProperty('runAllCheckers')
  shouldUseDelombok = project.hasProperty('useDelombok')
}

static String buildNumberToIntellijVersion(String buildNumber) {
  return '20' + buildNumber.substring(0, 2) + '.' + buildNumber.substring(2, 3)
}

static String intellijVersionToBuildNumber(String version) {
  return version.substring(2, 6).replace('.', '')
}

static String majorPartOfIntellijVersion(String version) {
  return version.substring(0, 6)
}

static Properties intellijVersionsProperties() {
  Properties properties = new Properties()
  properties.load(intellijVersionsFile().newDataInputStream())
  return properties
}

static File intellijVersionsFile() {
  return new File("intellijVersions.properties")
}

/**
 * @param versionKey Either release number (like 2020.3) or key of intellijVersions (like eapOfLatestSupportedMajor)
 * @returns Corresponding release numbers.
 */
List<String> resolveIntelliJVersions(String versionKey) {
  if (versionKey == null) {
    return []
  }
  if (versionKey =~ /^[0-9].*$/) {
    return [versionKey]
  }

  def versionValue = project.intellijVersions.get(versionKey)
  if (!versionValue) {
    return []
  }
  if (versionValue instanceof List) {
    return versionValue
  }
  return [versionValue]
}

import java.util.regex.Matcher
import java.util.regex.Pattern
import org.jsoup.Jsoup
import org.jsoup.nodes.Element
import org.jsoup.nodes.Document

String findNewestEapBuildNumber() {
  Document htmlContent = Jsoup.connect(project.intellijSnapshotsUrl).get()
  Element link = htmlContent.select('a[href~=' + project.intellijSnapshotsUrl + 'com/jetbrains/intellij/idea/ideaIC/\\d+\\.\\d+\\.\\d+][href$="-EAP-SNAPSHOT.pom"]')[0]
  if (link) {
    String attr = link.attr("href")
    Pattern pattern = Pattern.compile("(?<=ideaIC-)\\d+\\.\\d+\\.\\d+(?=-EAP-SNAPSHOT.pom)")
    Matcher matcher = pattern.matcher(attr)
    matcher.find()
    return matcher.group()
  }
  return null
}

String checkForEapWithBuildNumberHigherThan(String latestEapBuildNumber) {
  String foundBuildNumber = project.findNewestEapBuildNumber()
  if (foundBuildNumber) {
    if (VersionNumber.parse(foundBuildNumber) > VersionNumber.parse(latestEapBuildNumber)) {
      return foundBuildNumber
    }
  }
  return null
}

task updateEapBuildNumber {
  doLast {
    Properties properties = project.intellijVersionsProperties()
    File propFile = project.intellijVersionsFile()
    String latestEapBuildNumber = project.intellijVersions.eapOfLatestSupportedMajor
    String newerEapBuildNumber = latestEapBuildNumber ? project.checkForEapWithBuildNumberHigherThan(latestEapBuildNumber.replace('-EAP-SNAPSHOT', '')) :
            project.checkForEapWithBuildNumberHigherThan(intellijVersionToBuildNumber(project.intellijVersions.latestStable)+'.999999.999999')
    if (newerEapBuildNumber) {
      properties.setProperty("eapOfLatestSupportedMajor", newerEapBuildNumber+"-EAP-SNAPSHOT")
      properties.store(propFile.newWriter(), /*comments*/ null)
    }
  }
}

dependencyUpdates {
  def isStableVersion = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return stableKeyword || version ==~ regex
  }

  rejectVersionIf {
    !isStableVersion(it.candidate.version)
  }
}


import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

allprojects {
  repositories {
    mavenCentral()
  }

  apply plugin: 'org.gradle.java-library'

  sourceCompatibility = javaMajorVersion
  targetCompatibility = javaMajorVersion // redundant, added for clarity

  // String interpolation support, see https://github.com/antkorwin/better-strings
  // This needs to be enabled in each subproject by default because there's going to be no warning
  // if this annotation processor isn't run in any subproject (the strings will be just interpreted verbatim, without interpolation applied).
  // We'd only capture that in CI's post-compile checks by analyzing constants in class files.
  dependencies {
    def betterStringsCoords = [ group: 'com.antkorwin', name: 'better-strings', version: betterStringsVersion ]
    annotationProcessor(betterStringsCoords)
    testAnnotationProcessor(betterStringsCoords)
  }

  tasks.withType(JavaCompile) {
    options.compilerArgs += [
      '-AcallToStringExplicitlyInInterpolations', // Enforce explicit `.toString()` call in code generated for string interpolations
      '-Werror', // Treat each compiler warning (esp. the ones coming from Checker Framework) as an error.
      '-Xlint:unchecked', // Warn of type-unsafe operations on generics.
    ]

    options.fork = true
    options.forkOptions.jvmArgs += jvmArgsForJavaCompilation ?: (isCI ? [] : ['-Xmx6G', '-XX:+HeapDumpOnOutOfMemoryError'])

    // `sourceCompatibility` and `targetCompatibility` say nothing about the Java APIs available to the compiled code.
    // In fact, for X < Y it's perfectly possible to compile Java X code that uses Java Y APIs...
    // This will work fine, until we actually try to run those compiled classes under Java X-compatible JVM,
    // when we'll end up with NoSuchMethodError for APIs added between Java X and Java Y
    // (i.e. for X=8 and Y=11: InputStream#readAllBytes, Stream#takeWhile and String#isBlank).
    // `options.release = X` makes sure that regardless of Java version used to run the compiler,
    // only Java X-compatible APIs are available to the compiled code.
    options.release = Integer.parseInt(javaMajorVersion.majorVersion)
  }

  tasks.withType(Javadoc) {
    // See JDK-8200363 (https://bugs.openjdk.java.net/browse/JDK-8200363) for information about the `-Xwerror` option:
    // this is needed to make sure that javadoc always fails on warnings
    // (esp. important on CI since javadoc there for some reason seems to never raise any errors otherwise).

    // The '-quiet' as second argument is actually a hack around https://github.com/gradle/gradle/issues/2354:
    // since the one-parameter `addStringOption` doesn't seem to work, we need to add an extra `-quiet`, which is added anyway by Gradle.
    options.addStringOption('Xwerror', '-quiet')
    options.addStringOption('Xdoclint:all', '-quiet')
  }

  tasks.withType(Test) {
    testLogging {
      events TestLogEvent.FAILED
      exceptionFormat TestExceptionFormat.FULL
      showCauses true
      showExceptions true
      showStackTraces true
    }
  }


  apply plugin: 'com.diffplug.spotless'
  spotless {
    java {
      importOrder 'java', 'javax', '', 'com.virtuslab'
      // See https://github.com/diffplug/spotless/blob/master/ECLIPSE_SCREENSHOTS.md on importing and exporting settings from Eclipse
      eclipse().configFile "$rootDir/config/spotless/formatting-rules.xml"
      removeUnusedImports()
      targetExclude 'build/generated/**/*.*'
    }
  }

  if (!isCI) {
    compileJava.dependsOn 'spotlessApply'
  }


  apply plugin: 'org.checkerframework'
  checkerFramework {
    excludeTests = true
    checkers = [
      'org.checkerframework.checker.nullness.NullnessChecker',
    ]
    if (shouldRunAllCheckers) {
      // The experience shows that the below checkers are just rarely failing, as compared to GuiEffect/Nullness.
      // Hence, they're only applied in CI, or locally only if a dedicated Gradle project property is set.
      checkers += [
        'org.checkerframework.checker.index.IndexChecker',
        'org.checkerframework.checker.interning.InterningChecker',
        'org.checkerframework.checker.optional.OptionalChecker',
      ]
    }
    extraJavacArgs = [
      '-AassumeAssertionsAreEnabled',
      '-AinvariantArrays',
      '-Alint=cast:redundant,cast:unsafe',
      '-ArequirePrefixInWarningSuppressions',
      '-AshowSuppressWarningsStrings',
      "-Astubs=$rootDir/config/checker/",
      // The `-AstubWarnIfNotFoundIgnoresClasses` flag is required since Checker 3.14.0,
      // the version since which `-AstubWarnIfNotFound` is assumed to be true for custom stub files.
      // Without this flag, we would end up with a lot of errors in subprojects where any of the stubbed libraries is NOT on the classpath:
      // e.g. compilation of a subproject where Vavr is NOT a dependency would fail
      // since for simplicity, we're providing the same set of stubs to Checker in each subproject
      // (`$rootDir/config/checker/`, which includes e.g. Vavr).
      '-AstubWarnIfNotFoundIgnoresClasses',
      '-AsuppressWarnings=allcheckers:type.anno.before.decl.anno,allcheckers:type.anno.before.modifier,allcheckers:type.checking.not.run,value:annotation',
    ]
    dependencies {
      compileOnly      group: 'org.checkerframework', name: 'checker-qual', version: checkerFrameworkVersion
      checkerFramework group: 'org.checkerframework', name: 'checker',      version: checkerFrameworkVersion
    }
  }


  apply plugin: 'org.gradle.checkstyle'
  checkstyle {
    toolVersion checkstyleToolVersion

    checkstyleTest.enabled = false

    configProperties = [rootCheckstyleConfigDir: "$rootDir/config/checkstyle"]
  }


  if (shouldUseDelombok) {
    apply plugin: 'io.freefair.lombok'
    lombok {
      version = lombokVersion
      generateLombokConfig.enabled = false
    }
  }


  // A few libraries (like JGit and reflections) transitively pull in a version of slf4j-api
  // that might be different from the slf4j-api version that IntelliJ depends on.
  // SLF4J guarantees that the code compiled against a certain slf4j-api version will work with any other version of slf4j-api
  // (http://www.slf4j.org/manual.html#compatibility).
  // We rely on that guarantee: our plugin effectively uses whatever slf4j-api version is provided by IntelliJ.
  // SLF4J does NOT guarantee, however, that slf4j-api version X will work with any slf4j implementation version Y for X != Y.
  // To avoid a clash between JGit&co.'s slf4j-api and Intellij's slf4j implementation
  // (and also between JGit&co.'s slf4j-api and Intellij's slf4j-api), we need to exclude the former from ALL dependencies.
  configurations.runtimeClasspath {
    exclude group: 'org.slf4j', module: 'slf4j-api'
  }

  ext {
    addIntellijToCompileClasspath = { Map params ->
      apply plugin: 'org.jetbrains.intellij'

      // For the frontend subprojects we only use the Intellij plugin to provide dependencies,
      // but don't want the associated tasks to be available; they should only be available in the root project.
      buildPlugin.enabled = false
      buildSearchableOptions.enabled = false
      downloadRobotServerPlugin.enabled = false
      jarSearchableOptions.enabled = false
      patchPluginXml.enabled = false
      prepareSandbox.enabled = false
      prepareTestingSandbox.enabled = false
      prepareUiTestingSandbox.enabled = false
      publishPlugin.enabled = false
      runIde.enabled = false
      runIdeForUiTests.enabled = false
      verifyPlugin.enabled = false

      checkerFramework {
        // Technically, UI thread handling errors can happen outside of the (mostly frontend) modules that depend on IntelliJ,
        // but the risk is minuscule and not worth the extra computational burden in every single build.
        // This might change, however, if/when Checker Framework adds @Heavyweight annotation
        // (https://github.com/typetools/checker-framework/issues/3253).
        checkers += 'org.checkerframework.checker.guieffect.GuiEffectChecker'
      }

      intellij {
        version = intellijVersions.buildTarget
        // No need to instrument Java classes with nullability assertions, we've got this covered much better by Checker
        // (and we don't plan to expose any part of the plugin as an API for other plugins).
        instrumentCode = false
        if (params.withGit4Idea) {
          plugins = ['git4idea']
        }
      }
    }

    applyAliasingChecker = { ->
      if (shouldRunAllCheckers) {
        checkerFramework {
          checkers += 'org.checkerframework.common.aliasing.AliasingChecker'
        }
      }
    }

    applyI18nFormatterChecker = { ->
      // I18nFormatterChecker, like GuiEffectChecker and NullnessChecker, is enabled
      // regardless of `CI` env var/`runAllCheckers` Gradle project property.
      checkerFramework {
        checkers += 'org.checkerframework.checker.i18nformatter.I18nFormatterChecker'
        extraJavacArgs += '-Abundlenames=GitMacheteBundle'
      }

      // Apparently, I18nFormatterChecker doesn't see resource bundles in its classpath unless they're defined in a separate module.
      dependencies {
        checkerFramework project(':frontend:base')
      }
    }

    applySubtypingChecker = { ->
      if (shouldRunAllCheckers) {
        dependencies {
          checkerFramework project(':qual')
        }
        checkerFramework {
          checkers += 'org.checkerframework.common.subtyping.SubtypingChecker'
          def qualClassDir = project(':qual').sourceSets.main.output.classesDirs.asPath
          extraJavacArgs += "-ASubtypingChecker_qualDirs=${qualClassDir}"
        }
      }
    }

    applyKotlinConfig = { ->
      compileKotlin {
        // TODO (#785): revert this setting
        // kotlinOptions.allWarningsAsErrors = true
        kotlinOptions.jvmTarget = javaMajorVersion
      }

      def kotlinVersion = plugins.getPlugin(org.jetbrains.kotlin.gradle.plugin.KotlinPluginWrapper.class).kotlinPluginVersion
      dependencies {
        // kotlin-reflect is NOT added by Gradle Kotlin plugin 1.4/1.5/1.6 automatically,
        // even though it turns out to be needed in the runtime (e.g. in `DeleteBranchOnSlideOutSuggestionDialog`).
        // Note that kotlin-reflect pulls in kotlin-stdlib transitively, even if `kotlin.stdlib.default.dependency=false` is set in gradle.properties
        // (see https://plugins.jetbrains.com/docs/intellij/kotlin.html#kotlin-standard-library)
        implementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlinVersion
      }

      spotless {
        kotlin {
          ktfmt()
        }
      }
    }

    commonsIO = { ->
      dependencies {
        implementation group: 'commons-io', name: 'commons-io', version: commonsIOVersion
      }
    }

    ideProbe = { ->
      repositories {
        maven { url 'https://packages.jetbrains.team/maven/p/ij/intellij-dependencies' }
      }

      dependencies {
        uiTestImplementation project(':testCommon').sourceSets.test.output
        uiTestImplementation group: 'org.virtuslab.ideprobe', name: "junit-driver_$scalaMajorVersion", version: ideProbeVersion
        uiTestImplementation group: 'org.virtuslab.ideprobe', name: "robot-driver_$scalaMajorVersion", version: ideProbeVersion
      }
    }

    jcabiAspects = { ->
      apply plugin: 'io.freefair.aspectj.post-compile-weaving'

      compileJava {
        // Turn off `adviceDidNotMatch` spam warnings
        ajc.options.compilerArgs += '-Xlint:ignore'
      }

      dependencies {
        aspect group: 'com.jcabi', name: 'jcabi-aspects', version: jcabiAspectsVersion
      }
    }

    jetbrainsAnnotations = { ->
      dependencies {
        compileOnly     group: 'org.jetbrains', name: 'annotations', version: jetbrainsAnnotationsVersion
        testCompileOnly group: 'org.jetbrains', name: 'annotations', version: jetbrainsAnnotationsVersion
      }
    }

    jgit = { ->
      dependencies {
        implementation group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: jgitVersion
      }
    }

    junit = { ->
      dependencies {
        testImplementation group: 'junit', name: 'junit', version: junitVersion
      }
    }

    lombok = { ->
      // If delombok is turned on, then Lombok is provided for each subproject via Gradle plugin instead.
      // Applying Lombok plugin conditionally from within this `ext` block turned out to be troublesome.
      if (!shouldUseDelombok) {
        dependencies {
          def lombokCoords = [group: 'org.projectlombok', name: 'lombok', version: lombokVersion]
          compileOnly lombokCoords
          annotationProcessor lombokCoords
          testCompileOnly lombokCoords
          testAnnotationProcessor lombokCoords
        }
      }
    }

    powerMock = { ->
      dependencies {
        testImplementation group: 'org.powermock', name: 'powermock-module-junit4', version: powerMockVersion
        testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: powerMockVersion
      }
    }

    reflections = { ->
      dependencies {
        implementation group: 'org.reflections', name: 'reflections', version: reflectionsVersion
      }
    }

    slf4jLambdaApi = { ->
      dependencies {
        // It's so useful for us because we are using invocations of methods that potentially consume some time
        // also in debug messages, but this plugin allows us to use lambdas that generate log messages
        // (mainly using string interpolation plugin) and these lambdas are evaluated only when needed
        // (i.e. when the given log level is active)
        implementation group: 'kr.pe.kwonnam.slf4j-lambda', name: 'slf4j-lambda-core', version: slf4jLambdaApiVersion
      }
    }

    slf4jTestImpl = { ->
      // We only need to provide an SLF4J implementation in the contexts which depend on the plugin but don't depend on IntelliJ.
      // In our case, that's solely the tests of backend modules.
      // In other contexts that require an SLF4J implementation (buildPlugin, runIde, UI tests),
      // an SLF4J implementation is provided by IntelliJ.
      // Note that we don't need to agree the SLF4J implementation version here with slf4j-api version pulled in by our dependencies (like JGit)
      // since the latter is excluded (see the comment to `exclude group: 'org.slf4j'` for more nuances).
      // The below dependency provides both slf4j-api and an implementation, both already in the same version.
      // Global exclusion on slf4j-api does NOT apply to tests since it's only limited to `runtimeClasspath` configuration.
      dependencies {
        testRuntimeOnly group: 'org.slf4j', name: 'slf4j-simple', version: slf4jVersion
      }
    }

    vavr = { ->
      dependencies {
        // Unlike any other current dependency, Vavr classes are very likely to end up in binary interface of the depending subproject,
        // hence it's better to just treat Vavr as an `api` and not `implementation` dependency by default.
        api group: 'io.vavr', name: 'vavr', version: vavrVersion
      }
    }
  }
}


subprojects {
  // This is necessary to make sure that `buildPlugin` task puts jars of all relevant subprojects into the final zip.
  // No need to include near-empty (only with META-INF/MANIFEST.MF) jars
  // for subprojects that don't have any production code.
  if (sourceSets.main.allSource.srcDirs.findAll { file(it).exists() }) {
    rootProject.dependencies {
      implementation(project)
    }
  }

  // By default, the jar name will be formed only from the last segment of subproject path.
  // Since these last segments are NOT unique (there are many `api`s and `impl`s),
  // the effective jar name will be something like api.jar, api_1.jar, api_2.jar etc.,
  // which is suboptimal.
  // Let's use full name like frontend-ui-api.jar instead.
  archivesBaseName = path.replaceFirst(":", "").replaceAll(":", "-")
}


// Root project config

group 'com.virtuslab'

apply plugin: 'org.ajoberstar.grgit'
apply from: 'version.gradle'

if (ciBranch == 'master') {
  version PROSPECTIVE_RELEASE_VERSION  // more precisely, "soon-to-happen-in-this-pipeline release version" in case of master builds

} else if (!file('.git').exists()) {
  // To make sure it's safe for Docker image builds where .git folder is unavailable
  version "${PROSPECTIVE_RELEASE_VERSION}-SNAPSHOT"

} else {
  def maybeSnapshot = ciBranch == 'develop' ? '' : '-SNAPSHOT'

  def git = org.ajoberstar.grgit.Grgit.open(currentDir: projectDir)
  def lastTag = git.tag.list().sort { it.dateTime }.last()
  def commitsSinceLastTag = grgit.log(includes: ['HEAD'], excludes: [lastTag.fullName])
  def maybeCommitCount = commitsSinceLastTag.empty ? '' : '-' + commitsSinceLastTag.size()
  def shortCommitHash = git.head().abbreviatedId
  def maybeDirty = git.status().clean ? '' : '-dirty'
  git.close()

  version "${PROSPECTIVE_RELEASE_VERSION}${maybeCommitCount}${maybeSnapshot}+git.${shortCommitHash}${maybeDirty}"
}



dependencies {
  // Checker is needed on root project runtime (not just compile-time) classpath for ArchUnit tests
  testImplementation group: 'org.checkerframework', name: 'checker-qual', version: checkerFrameworkVersion

  testImplementation group: 'com.tngtech.archunit', name: 'archunit', version: archUnitVersion
}


apply plugin: 'org.jetbrains.intellij'

intellij {
  instrumentCode = false
  pluginName = 'git-machete-intellij-plugin'
  version = intellijVersions.buildTarget

  plugins = ['git4idea'] // Needed solely for ArchUnit
}

if (!isCI) {
  // The output of this task is for some reason very poorly cached,
  // and the task takes a significant amount of time,
  // while the index of searchable options is of little use for local development.
  buildSearchableOptions.enabled = false
}

patchPluginXml {
  // `sinceBuild` is exclusive when we are using `*` in version but inclusive when without `*`
  sinceBuild = intellijVersionToBuildNumber(intellijVersions.earliestSupportedMajor)

  // In `untilBuild` situation is inverted: it's inclusive when using `*` but exclusive when without `*`
  untilBuild = intellijVersionToBuildNumber(intellijVersions.latestSupportedMajor) + '.*'

  // Note that the first line of the description should be self-contained since it is placed into embeddable card:
  // see e.g. https://plugins.jetbrains.com/search?search=git%20machete
  pluginDescription = file("$rootDir/DESCRIPTION.html").text

  changeNotes = "<h3>v${rootProject.version}</h3>\n\n" + file("$rootDir/CHANGE-NOTES.html").text
}

runIde {
  maxHeapSize = '4G'
}

import static org.jetbrains.intellij.tasks.RunPluginVerifierTask.FailureLevel

runPluginVerifier {
  def maybeEap = intellijVersions.eapOfLatestSupportedMajor
    ? [intellijVersions.eapOfLatestSupportedMajor.replaceAll('-EAP-(CANDIDATE-)?SNAPSHOT', '')]
    : []
  ideVersions = [
    *intellijVersions.latestMinorsOfOldSupportedMajors,
    intellijVersions.latestStable,
    *maybeEap
  ]

  def skippedFailureLevels = EnumSet.of(FailureLevel.DEPRECATED_API_USAGES, FailureLevel.EXPERIMENTAL_API_USAGES, FailureLevel.NOT_DYNAMIC)
  failureLevel = EnumSet.complementOf(skippedFailureLevels)
}

publishPlugin {
  token = jetbrainsMarketplaceToken
}


def uiTest = sourceSets.create('uiTest')

configurations {
  uiTestImplementation.extendsFrom testImplementation
  uiTestRuntimeOnly.extendsFrom testRuntimeOnly
}

List<String> uiTestTargets = project.properties.against \
  ? resolveIntelliJVersions(project.properties.against)
  : [intellijVersions.buildTarget]

uiTestTargets.each { version ->
  tasks.register("uiTest_${version}", Test) {
    description = 'Runs UI tests.'
    group = 'verification'

    testClassesDirs = uiTest.output.classesDirs
    classpath = sourceSets.main.output + configurations.uiTestRuntimeClasspath + uiTest.output
    // Let's keep the XML test results in a per-version directory, this makes things easier in the CI
    // where the UI tests are executed under a couple different IDE versions one after another.
    // The results in the default directory (build/test-results/uiTest) would get overwritten which each subsequent test.
    reports.junitXml.destination = file("$buildDir/test-results/uiTest/${version}")

    dependsOn ':buildPlugin'

    systemProperty 'ui-test.intellij.version', version
    systemProperty 'ui-test.plugin.path', buildPlugin.outputs.files[0].path

    if (project.properties.tests) {
      filter {
        includeTestsMatching "*.*${project.properties.tests}*"
      }
    }

    if (project.hasProperty('headless')) {
      environment 'IDEPROBE_DISPLAY', 'xvfb'
    }

    testLogging {
      events += [TestLogEvent.STANDARD_OUT, TestLogEvent.STANDARD_ERROR]
    }
  }
}

// Cannot use `task uiTest(type: Test)` syntax due to the name conflict with `def uiTest` above.
tasks.register('uiTest') {
  dependsOn tasks.matching { Task task -> task.name.startsWith("uiTest_") }
}

ideProbe()
