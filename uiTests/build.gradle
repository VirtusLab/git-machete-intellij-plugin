import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

plugins {
  id 'org.gradle.scala'
}

ideProbe()
jetbrainsAnnotations()
junit()
lombok()
scala()
vavr()

dependencies {
  testImplementation project(':testCommon').sourceSets.test.output
}

task syncTestResults(type: Sync) {
  from testResultsDir

  def subdirectoryName = project.properties.testResultsSubdir
    ?: LocalDateTime.now().format(DateTimeFormatter.ofPattern("yyyy-MM-dd_HH-mm-ss"))
  into "$buildDir/ui-test-results/$subdirectoryName"
}

test {
  // Exclude UI tests when running `./gradlew test`, unless a `-PrunUiTests` Gradle flag is provided.
  def runUiTests = project.hasProperty('enableUiTests')

  onlyIf {
    runUiTests
  }
  if (runUiTests) {
    dependsOn ':buildPlugin'
  }

  systemProperty 'intellij.version', project.findProperty('intellijVersion')

  // Always re-run the tests, never assume they're up to date.
  outputs.upToDateWhen { false }

  // Let's copy the XML test results to a unique per-run directory, this makes things easier in the CI
  // where the UI tests are executed under a couple different IDE versions one after another.
  // The results in the default directory (build/test-results/) are getting overwritten which each subsequent test.
  finalizedBy(syncTestResults)
}
